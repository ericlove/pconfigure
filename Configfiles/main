LANGUAGES   += c
COMPILEOPTS += -Wall
COMPILEOPTS += -Wextra
COMPILEOPTS += -Werror
COMPILEOPTS += -Wno-trampolines
COMPILEOPTS += -Wno-unused-parameter
COMPILEOPTS += -std=gnu99
COMPILEOPTS += -DPCONFIGURE_VERSION=\"0.3.0git\"
LANGUAGES   += bash

BINARIES    += pconfigure
LINKOPTS    += `pllvm-config --optional --have CLANG --libs`
COMPILEOPTS += `pllvm-config --optional --have CLANG --cflags`
LINKOPTS    += `ppkg-config  --optional --have TALLOC talloc --libs`
COMPILEOPTS += `ppkg-config  --optional --have TALLOC talloc --cflags`
COMPILEOPTS += -Isrc/extern/
DEPLIBS     += pinclude
SOURCES     += pconfigure/main.c

BINARIES    += pclean
SOURCES	    += pclean.bash
TESTSRC     += simple.bash

BINARIES    += pbashc
SOURCES	    += pbashc.c

BINARIES    += ppkg-config
SOURCES	    += ppkg-config.bash

BINARIES    += pgcc-config
SOURCES     += pgcc-config.bash

BINARIES    += pscalac
SOURCES	    += pscala/compiler.bash

BINARIES    += pscalald
SOURCES	    += pscala/linker.bash

BINARIES    += pnasm
SOURCES	    += pnasm.bash

BINARIES    += ptest
SOURCES	    += ptest.bash

LIBRARIES   += libpinclude.so
COMPILEOPTS += -fPIC
LINKOPTS    += -fPIC
SOURCES     += libpinclude/library.c

BINARIES    += pinclude
DEPLIBS     += pinclude
SOURCES     += pinclude.c
TESTSRC     += empty.bash
TESTSRC     += nonexist.bash
TESTSRC     += samedir.bash
TESTSRC     += samedir2.bash
TESTSRC     += indir.bash

BINARIES    += pllvm-config
SOURCES     += pllvm-config.bash

BINARIES    += pwinegcc
SOURCES	    += pwinegcc.bash

